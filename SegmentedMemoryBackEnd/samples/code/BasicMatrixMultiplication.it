// A simple single-space matrix multiplication task that compute c as the multiplication
// of a and b.
Task "Matrix Multiply":
	Define:
		a, b, c: 2D Array of Real double-precision
	Environment:
		a, b: link
		c: create
	Initialize:
		c.dimension1 = a.dimension1
		c.dimension2 = b.dimension2
	Compute:
		// no name is given to the computation here as that would be redundant
		"" (Space A) { 
			do { c[i][j] = c[i][j] + a[i][k] * b[k][j]
			} for i, j in c; k in a
		}
	Partition (k, l):
		// 2D partitioning with each partition computing a block of C.
		// Rows of A are replicated along the second and columns of B
		// are replicated along the first dimension.
		Space A <2D> {
			c: block_size (k, l)
			a: block_size (k), replicated
			b: replicated, block_size (l)
		}
