Task "Matrix Multiply":
	Define:
		a, b, c: 2D Array of Real double-precision
	Environment:
		a, b: link
		c: create
	Initialize:
		c.dimension1 = a.dimension1
		c.dimension2 = b.dimension2
	Compute:
		"Block multiply matrices" (Space C) { 
			do { do { do { 
					c[i][j] = c[i][j] + a[i][k] * b[k][j]
				     } for k in a
			     } for i in a	 	
			} for j in b
		}
		Repeat: from "Block multiply matrices" foreach Space C sub-partition
	Partition (k, l, q):
		Space A <2D> {
			c: block_count(2, 2)
			a: block_count(2), replicated
			b: replicated, block_count(2)
		}
		Space B <1D> divides Space A partitions {
			c<dim1>, a<dim1>: block_count(2)
			b: replicated
		}	
		Space C <2D> divides Space B partitions {
			c: block_size(k, l)
			a: block_size(k), replicated
			b: replicated, block_size(l)
			Sub-partition <1D> <unordered> {
				a<dim2>, b<dim1>: block_size(q)
			}
		}
